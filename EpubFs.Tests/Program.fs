module Tests

open EpubFs
open EpubFs.Write
open Expecto
open Giraffe.ViewEngine
open System
open System.IO
open System.IO.Compression
open System.Text

#nowarn 760

let commonModifiedAt = DateTimeOffset (2010, 10, 10, 7, 0, 0, TimeSpan.FromHours 7.0) |> Some

let css = Encoding.UTF8.GetBytes ".red { color: red } h1 { font-style: italic }"

let compareEpubs (generated: Stream) (samplePath: string) =
    generated.Position <- 0L
    use generated = new ZipArchive (generated)
    use sample = File.OpenRead (Path.Combine (__SOURCE_DIRECTORY__, "samples", samplePath))
    use sample = new ZipArchive (sample)

    let generatedEntries = generated.Entries |> Seq.sortBy (fun x -> x.FullName) |> Seq.toArray
    let sampleEntries = sample.Entries |> Seq.sortBy (fun x -> x.FullName) |> Seq.toArray

    for generated, sample in Array.zip generatedEntries sampleEntries do
        Expect.equal generated.FullName sample.FullName "Epubs contain files with different names"

        use generatedStream = generated.Open ()
        use sampleStream = sample.Open ()

        Expect.streamsEqual generatedStream sampleStream $"Generated and sample {sample.FullName} files are not equal"

let writeTests = testList "WriteTests" [
    test "No cover, auto nav, no css" {
        use generated = new MemoryStream ()
        let title = { FileName = "title.xhtml"; Title = "Title page"; Input = Structured [ h1 [] [ str "Odyssey" ] ]; Navigation = None; Smil = None }
        let p1 = { FileName = "p1.xhtml"; Title = "Page one"; Input = Structured [ div [] [ str "Ἄνδρα μοι ἔννεπε, Μοῦσα, πολύτροπον, ὃς μάλα πολλὰ" ]; div [] [ str "πλάγχθη, ἐπεὶ Τροίης ἱερὸν πτολίεθρον ἔπερσε·" ] ]; Navigation = Some Linear; Smil = None }
        let p2 = { FileName = "p2.xhtml"; Title = "Page two"; Input = Structured [ h2 [] [ str "yup again" ] ]; Navigation = Some Linear; Smil = None }

        let metadata = { Id = "54645-3231-54"; Title = "Ὀδύσσεια"; Languages = [ "en"; "grc" ]; ModifiedAt = commonModifiedAt; Source = None; Creators = [ "Ὅμηρος" ]; Description = None; Publisher = None; Subjects = []; Rights = None; MediaOverlay = None }
        write generated metadata { CoverImage = None; TitlePage = title; NavigationFile = Autogenerated; ContentFiles = [ p1; p2 ]; CssFiles = []; OtherFiles = [] }
        
        //generated.Position <- 0L
        //File.WriteAllBytes (__SOURCE_DIRECTORY__ + "/samples/No cover, auto nav, no css.epub", generated.ToArray ())
        compareEpubs generated "No cover, auto nav, no css.epub"
    }

    test "Cover, auto nav, css" {
        use p1 =
            """<!DOCTYPE html><html xmlns="http://www.w3.org/1999/xhtml"><head><link href="main.css" type="text/css" rel="stylesheet"/><title>Sida ett</title></head><body><div class="red">Jag ligger på kabelgattet</div><div>rökande "Fem blå bröder"</div></body></html>"""
            |> Encoding.UTF8.GetBytes
            |> MemoryStream

        use generated = new MemoryStream ()
        let title = { FileName = "title.xhtml"; Title = "Solnedgång på havet"; Input = Structured [ h1 [] [ str "Solnedgång på havet" ] ]; Navigation = None; Smil = None }
        let p1 = { FileName = "p1.xhtml"; Title = "Sida ett"; Input = ContentInput.Raw p1; Navigation = Some Linear; Smil = None }
        let p2 = { FileName = "p2.xhtml"; Title = "Sida två"; Input = Structured [ div [ _class "red" ] [ str "och tänker på intet." ] ]; Navigation = Some Linear; Smil = None }
        use cover = File.OpenRead (__SOURCE_DIRECTORY__ + "/samples/cover.jpg")

        let metadata = { Id = "54645-3231-55"; Title = "Solnedgång på havet"; Languages = [ "sv" ]; ModifiedAt = commonModifiedAt; Source = Some "https://sv.wikisource.org/wiki/Solnedg%C3%A5ng_p%C3%A5_havet"; Creators = [ "August Strindberg" ]; Description = Some "A beautiful poem about sunset at sea."; Publisher = Some "Swedish Poetry Press"; Subjects = [ "Poetry"; "Swedish Literature" ]; Rights = Some "Public Domain"; MediaOverlay = None }
        write generated metadata { CoverImage = Some { Input = cover; MediaType = "image/jpeg"; Extension = ".jpg" }; TitlePage = title; NavigationFile = Autogenerated; ContentFiles = [ p1; p2 ]; CssFiles = [ { FileName = "main.css"; Input = new MemoryStream (css) } ]; OtherFiles = [] }
        
        //generated.Position <- 0L
        //File.WriteAllBytes (__SOURCE_DIRECTORY__ + "/samples/Cover, auto nav, css.epub", generated.ToArray ())
        compareEpubs generated "Cover, auto nav, css.epub"
    }

    test "Media overlay with SMIL" {
        use generated = new MemoryStream ()
        
        let smil1 = {
            Input = SmilInput.ParNodes ("audio.mp3", [
                { TextFragmentReference = "#line1"; ClipBegin = Choice1Of2 "00:00:00"; ClipEnd = Choice1Of2 "00:00:00.300" }
                { TextFragmentReference = "#line2"; ClipBegin = Choice1Of2 "00:00:01.300"; ClipEnd = Choice1Of2 "00:00:03.000" }
                { TextFragmentReference = "#line3"; ClipBegin = Choice1Of2 "00:00:04"; ClipEnd = Choice1Of2 "00:00:04.500" }
            ])
            Duration = Choice2Of2 (TimeSpan.FromSeconds 4.5)
        }

        let title = { FileName = "title.xhtml"; Title = "Audio Book"; Input = Structured [ h1 [] [ str "Audio Book" ] ]; Navigation = None; Smil = None }
        let p1 = { 
            FileName = "p1.xhtml"
            Title = "Chapter 1"
            Input = Structured [ 
                div [ _id "line1" ] [ str "Hello." ]
                div [ _id "line2" ] [ str "Welcome to this audio book." ]
                div [ _id "line3" ] [ str "Good bye." ]
            ]
            Navigation = Some Linear
            Smil = Some smil1 
        }
        let p2 = { 
            FileName = "p2.xhtml"
            Title = "Chapter 2"
            Input = Structured [ 
                div [ _id "line1" ] [ str "This is chapter two." ]
            ]
            Navigation = Some Linear
            Smil = None 
        }

        let mediaOverlayCss = new MemoryStream (".-epub-media-overlay-active { background-color: yellow; }"B)
        let cssFile = { FileName = "media-overlay.css"; Input = mediaOverlayCss }
        use audioData = File.OpenRead (__SOURCE_DIRECTORY__ + "/samples/hello.mp3")
        let audio = { FileName = "audio.mp3"; MediaType = "audio/mpeg"; Input = audioData; Compress = false }

        let mediaOverlay = {
            TotalDuration = Choice2Of2 (TimeSpan.FromSeconds 4.5)
            ActiveClass = Some "-epub-media-overlay-active"
            PlaybackActiveClass = None
            Narrators = [ "Test Narrator" ]
        }

        let metadata = { Id = "smil-test-001"; Title = "Audio Book"; Languages = [ "en" ]; ModifiedAt = commonModifiedAt; Source = None; Creators = [ "Test Author" ]; Description = None; Publisher = None; Subjects = []; Rights = None; MediaOverlay = Some mediaOverlay }
        write generated metadata { CoverImage = None; TitlePage = title; NavigationFile = Autogenerated; ContentFiles = [ p1; p2 ]; CssFiles = [ cssFile ]; OtherFiles = [ audio ] }

        //generated.Position <- 0L
        //File.WriteAllBytes (__SOURCE_DIRECTORY__ + "/samples/Media overlay with SMIL.epub", generated.ToArray ())
        compareEpubs generated "Media overlay with SMIL.epub"
    }

]

[<EntryPoint>]
let main args =
    runTestsWithCLIArgs [] args writeTests