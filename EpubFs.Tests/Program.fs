module Tests

open EpubFs
open EpubFs.Write
open Expecto
open Giraffe.ViewEngine
open System
open System.IO
open System.IO.Compression
open System.Text

let commonModifiedAt = DateTimeOffset (2010, 10, 10, 0, 0, 0, TimeSpan.Zero) |> Some

let css = Encoding.UTF8.GetBytes ".red { color: red } h1 { font-style: italic }"

let compareEpubs (generated: Stream) (samplePath: string) =
    generated.Position <- 0L
    use generated = new ZipArchive (generated)
    use sample = File.OpenRead samplePath
    use sample = new ZipArchive (sample)

    let generatedEntries = generated.Entries |> Seq.sortBy (fun x -> x.FullName) |> Seq.toArray
    let sampleEntries = sample.Entries |> Seq.sortBy (fun x -> x.FullName) |> Seq.toArray

    for generated, sample in Array.zip generatedEntries sampleEntries do
        Expect.equal generated.FullName sample.FullName "Epubs contain files with different names"

        use generatedStream = generated.Open ()
        use sampleStream = sample.Open ()

        Expect.streamsEqual generatedStream sampleStream $"Generated and sample {sample.FullName} files are not equal"

let writeTests = testList "WriteTests" [
    test "No cover, auto nav, no css" {
        use generated = new MemoryStream ()
        let title = { FileName = "title.xhtml"; Title = "Title page"; Input = Structured [ h1 [] [ str "Odyssey" ] ]; Navigation = None }
        let p1 = { FileName = "p1.xhtml"; Title = "Page one"; Input = Structured [ div [] [ str "Ἄνδρα μοι ἔννεπε, Μοῦσα, πολύτροπον, ὃς μάλα πολλὰ" ]; div [] [ str "πλάγχθη, ἐπεὶ Τροίης ἱερὸν πτολίεθρον ἔπερσε·" ] ]; Navigation = Some Linear }
        let p2 = { FileName = "p2.xhtml"; Title = "Page two"; Input = Structured [ h2 [] [ str "yup again" ] ]; Navigation = Some Linear }

        let metadata = { Id = "54645-3231-54"; Title = "Ὀδύσσεια"; Languages = [ "en"; "grc" ]; ModifiedAt = commonModifiedAt; Source = None; Creators = [ "Ὅμηρος" ] }
        write generated metadata { CoverImage = None; TitlePage = title; NavXhtml = Autogenerated; ContentFiles = [ p1; p2 ]; CssFiles = [] }
        
        compareEpubs generated "samples/No cover, auto nav, no css.epub"
    }

    test "Cover, auto nav, css" {
        use p1 =
            """<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN" "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd"><html xmlns="http://www.w3.org/1999/xhtml"><head><link href="main.css" type="text/css" rel="stylesheet"/><title>Sida ett</title></head><body><div class="red">Jag ligger på kabelgattet</div><div>rökande "Fem blå bröder"</div></body></html>"""
            |> Encoding.UTF8.GetBytes
            |> MemoryStream

        use generated = new MemoryStream ()
        let title = { FileName = "title.xhtml"; Title = "Solnedgång på havet"; Input = Structured [ h1 [] [ str "Solnedgång på havet" ] ]; Navigation = None }
        let p1 = { FileName = "p1.xhtml"; Title = "Sida ett"; Input = ContentInput.Raw (RawInput.Stream p1); Navigation = Some Linear }
        let p2 = { FileName = "p2.xhtml"; Title = "Sida två"; Input = Structured [ div [ _class "red" ] [ str "och tänker på intet." ] ]; Navigation = Some Linear }
        use cover = File.OpenRead "samples/cover.jpg"

        let metadata = { Id = "54645-3231-55"; Title = "Solnedgång på havet"; Languages = [ "sv" ]; ModifiedAt = commonModifiedAt; Source = Some "https://sv.wikisource.org/wiki/Solnedg%C3%A5ng_p%C3%A5_havet"; Creators = [ "August Strindberg" ] }
        write generated metadata { CoverImage = Some { Input = RawInput.Stream cover; MediaType = "image/jpeg" }; TitlePage = title; NavXhtml = Autogenerated; ContentFiles = [ p1; p2 ]; CssFiles = [ { FileName = "main.css"; Input = RawInput.Stream (new MemoryStream (css)) } ] }
        
        compareEpubs generated "samples/Cover, auto nav, css.epub"
    }
]

[<EntryPoint>]
let main args =
    runTestsWithCLIArgs [] args writeTests